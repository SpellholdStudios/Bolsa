BACKUP ~bolsa/backup~
//AUTHOR ~www.shsforums.net/index.php?showforum=234~ //aimainahikari@hotmail.com
SUPPORT ~Spellhold Studios at http://www.shsforums.net/forum/234-miscellaneous-released-mods/~

VERSION ~v6.0.0~

README ~bolsa/readme/bolsa-readme-%LANGUAGE%.txt~ ~bolsa/readme/bolsa-readme-english.txt~

ALWAYS

	/* -------------------------------------------------------------------------------- *
	 *    Determines which game is installed and if the game is an Enhanced Edition.    *
	 * -------------------------------------------------------------------------------- */
	OUTER_SET is_ee      = (GAME_IS ~bg2ee eet~) ? 1 : 0		// EE game

	/* ----------------- *
	 *    Detects 1PP    *
	 * ----------------- */
	OUTER_SET is_1pp_staff  = ((MOD_IS_INSTALLED ~1pp.tp2~ ~207~) AND (MOD_IS_INSTALLED ~1pp.tp2~ ~400~)) ? 1 : 0	// 1ppv4: Wizards' Staves (core)
	OUTER_SET is_1pp_staff0 = ((MOD_IS_INSTALLED ~1pp.tp2~ ~204~) AND (is_1pp_staff = 0)) ? 1 : 0					// 1ppv4: Colourable Quarterstaves without 1ppv4: Wizards' Staves (core)

	/* -------------------------------- *
	 *    Loads macros and functions    *
	 * -------------------------------- */
	INCLUDE ~%MOD_FOLDER%/lib/gw_functions.tpa~				// Loads macros and functions library dealing with tooltip appending, specific EE items descriptions and item restriction flags.

	ACTION_IF (is_ee OR is_1pp_staff OR is_1pp_staff0) BEGIN
		INCLUDE ~%MOD_FOLDER%/lib/1pp_macros.tpa~			// Loads 1PP macros dealing with items coloring.
		LAM ~Locations~
		INCLUDE ~%MOD_FOLDER%/lib/1pp_compatibility.tph~	// Loads functions restoring items coloration for EE games or if 1PP relevant components are installed.
	END

	/* --------------------------------------------------------------- *
	 *    Adds all ToB actions in action.ids unless already present    *
	 * --------------------------------------------------------------- */
	ACTION_IF GAME_IS ~soa~ THEN BEGIN
		APPEND ~ACTION.IDS~ ~31 SpellRES(S:RES*,O:Target*)~					 UNLESS ~31 SpellRES(S:RES\*,O:Target\*)~
		APPEND ~ACTION.IDS~ ~95 SpellPointRES(S:RES*,P:Target*)~			 UNLESS ~95 SpellPointRES(S:RES\*,P:Target\*)~
		APPEND ~ACTION.IDS~ ~113 ForceSpellRES(S:RES*,O:Target)~			 UNLESS ~113 ForceSpellRES(S:RES\*,O:Target)~
		APPEND ~ACTION.IDS~ ~114 ForceSpellPointRES(S:RES*,P:Target)~		 UNLESS ~114 ForceSpellPointRES(S:RES\*,P:Target)~
		APPEND ~ACTION.IDS~ ~160 ApplySpellRES(S:RES*,O:Target)~			 UNLESS ~160 ApplySpellRES(S:RES\*,O:Target)~
		APPEND ~ACTION.IDS~ ~181 ReallyForceSpellRES(S:RES*,O:Target)~		 UNLESS ~181 ReallyForceSpellRES(S:RES\*,O:Target)~
		APPEND ~ACTION.IDS~ ~191 SpellNoDecRES(S:RES*,O:Target*)~			 UNLESS ~191 SpellNoDecRES(S:RES\*,O:Target\*)~
		APPEND ~ACTION.IDS~ ~192 SpellPointNoDecRES(S:RES*,P:Target*)~		 UNLESS ~192 SpellPointNoDecRES(S:RES\*,P:Target\*)~
		APPEND ~ACTION.IDS~ ~240 ReallyForceSpellDeadRES(S:RES*,O:Target)~	 UNLESS ~240 ReallyForceSpellDeadRES(S:RES\*,O:Target)~
		APPEND ~ACTION.IDS~ ~318 ForceSpellRangeRES(S:RES*,O:Target)~		 UNLESS ~318 ForceSpellRangeRES(S:RES\*,O:Target)~
		APPEND ~ACTION.IDS~ ~319 ForceSpellPointRangeRES(S:RES*,P:Target)~	 UNLESS ~319 ForceSpellPointRangeRES(S:RES\*,P:Target)~
		APPEND ~ACTION.IDS~ ~337 ReallyForceSpellPointRES(S:RES*,P:Target*)~ UNLESS ~337 ReallyForceSpellPointRES(S:RES\*,P:Target\*)~
	END

	/* --------------------------------------------------------------------------------------------- *
	 *    Convert strings to UTF-8 for BGEE/BG2EE                                                    *
	 *    No exception, everything needs to be converted                                             *
	 *    setup.tra contains in game texts as well as installation texts and needs to be converted   *
	 * --------------------------------------------------------------------------------------------- */
	ACTION_DEFINE_ARRAY mh_reload BEGIN setup END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
	ACTION_DEFINE_ARRAY mh_noconvert BEGIN END
	LAF HANDLE_CHARSETS
		INT_VAR
			infer_charsets = 1
		STR_VAR
			tra_path = EVAL ~%MOD_FOLDER%/tra~
			noconvert_array = mh_noconvert
			reload_array = mh_reload
	END

	LAM ~GW_ADJUST_COL_TOOLTIP~					// Staff of Magus needs 4 tooltips strref columns
	LAM ~GW_READ_COL_TOOLTIP~					// Reads how many columns are in tooltip.2da file.

END


AUTO_TRA ~bolsa/tra/%s~

LANGUAGE ~Espanol~
         ~spanish~
         ~bolsa/tra/spanish/setup.tra~

LANGUAGE ~English (translation by Lollorian)~
         ~english~
         ~bolsa/tra/english/setup.tra~

LANGUAGE ~Francais (traduction : Elgaern)~
         ~french~
         ~bolsa/tra/french/setup.tra~

LANGUAGE ~Italiano (traduzione di ilot)~
         ~italian~
         ~bolsa/tra/italian/setup.tra~


/* ====================== *
 *         Bolsa          *
 * ====================== */
BEGIN @0
DESIGNATED 0 LABEL "bolsa"
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ @1	// ~You must have BG2, ToB, BGT, BG2:EE or EET installed to install Bolsa mod.~


// Copy BAMs (certainly useless, but there is no harm to keep it...)
// -----------------------------------------------------------------
ACTION_IF NOT FILE_EXISTS_IN_GAME ~ibag05.bam~ BEGIN
	COPY ~bolsa/bam~ ~override~
END

// Install bags
// ------------
COPY_EXISTING ~bag02.sto~ ~override/mhbag02.sto~
              ~bag02.itm~ ~override/mhbag02.itm~
              ~bag03.sto~ ~override/mhbag03.sto~
              ~bag03.itm~ ~override/mhbag03.itm~
              ~bag04.sto~ ~override/mhbag04.sto~
              ~bag04.itm~ ~override/mhbag04.itm~

// Ammo Belt (Cinturon de munición
COPY ~bolsa/store/mhbag05.sto~ ~override~ 
	SAY NAME2 @15

COPY ~bolsa/itm/mhbag05.itm~ ~override~
	SAY NAME1 @15
	SAY NAME2 @15
	SAY UNIDENTIFIED_DESC @16
	SAY DESC @16

// Potion Case (Caja de pociones)
COPY ~bolsa/store/mhbag06.sto~ ~override~
	SAY NAME2 @17

COPY ~bolsa/itm/mhbag06.itm~ ~override~
	SAY NAME1 @17
	SAY NAME2 @17
	SAY UNIDENTIFIED_DESC @18
	SAY DESC @18

// Staff of Magus (Bastón de magus)
COPY ~bolsa/itm/mhconti.itm~ ~override~
	SAY NAME1 @11
	SAY NAME2 @11
	LPF ~GW_WRITE_EE_ITM_DESCRIPTIONS~ STR_VAR GW_desc_to_update = "@12" END
	PATCH_IF (is_ee) BEGIN
		LPF ~GW_ALTER_ITEM_ALTER_HEADER_FLAGS~ INT_VAR type = 3 header = 2 flag_breaksanctuary = 1 END
	END
	PATCH_IF (is_ee OR is_1pp_staff) BEGIN
		LPF ~1pp_staf11~ END
	END
	PATCH_IF (is_1pp_staff0) BEGIN
		LPF ~1pp_staf11_0~ END
	END
BUT_ONLY
LAF ~GW_ADD_ITEM_TOOLTIPS~ STR_VAR GW_objet = ~mhconti~ GW_tooltips = ~15529 3895 26304 @121~ END	// Melee - Fireball-Lightning 3 times per day - Spell Trap once per day - Mordekainen's Contingency once per day 
COPY_EXISTING ~tooltip.2da~ ~override~
	PRETTY_PRINT_2DA

// Mordekainen's Contingency (Lanzador Contingencia en Cadena Mejorada)
COPY ~bolsa/cre/mhbolsa2.cre~ ~override~
	SAY NAME1 @13
	SAY NAME2 @13
BUT_ONLY

COMPILE ~bolsa/baf/mhbolsa2.baf~

// Merchant (Vendedor) and New store (Tienda nueva)
// ------------------------------------------------
COPY ~bolsa/cre/mhbolsa.cre~ ~override~
	SAY NAME1 @14
	SAY NAME2 @14
BUT_ONLY

COPY ~bolsa/store/mhbolsa.sto~ ~override~
	SAY NAME2 @0
BUT_ONLY

// Compile dialog (Dialogo del vendedor)
// -------------------------------------
COMPILE ~bolsa/dlg/mhbolsa.d~

EXTEND_BOTTOM ~ar0511.bcs~ ~bolsa/baf/mhbolsa.baf~
